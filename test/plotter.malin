
fun main(): i32 {
  // start
  printNextLine();
  printLine();
  printNextLine();

  // plot x^2 from -11 to 11
  plotFunction(start=-11, end=11);

  // end
  printNextLine();
  printLine();
  printNextLine();
  return 0;
}



/**
 * Plot function x^2
 */
fun plotFunction(start: i32, end: i32) {
  // for each x from start to end
  let x = start;
  while x <= end {
    let funcVal = x * x;
    // print counter value and funcVal
    // counter : funcVal  -------
    printNumbersDigits(x);
    printColon();
    printTab();
    printTab();
    printBar(funcVal);
    printNextLine();
    // next
    x = x+1;
  }
}

fun printBar(value: i32) {
  if (value != 0 && !(value < 0)) {
    printMinus();
    printBar(value - 1);
  }
}



/**
 * Print a integer to the console.
 */
fun printNumber(number: i32) {
  printNumbersDigits(number);
  printNextLine();
}


/**
 * Print digits of an integer to the console.
 */
fun printNumbersDigits(number: i32) {
  // tread negative
  if number < 0 {
    number = number * -1;
    printMinus();
  }

  // print lower digit
  if number >= 10 {
    printNumbersDigits(number / 10);
  }

  // get lowest digit
  let digit = modulo(number, by= 10);
  // print one digit 0..9
  if (0 <= digit && digit <= 9) {
    putChar(c = digit + 48);
  }
}


fun printNextLine() {
  putChar(10);
}

fun printTab() {
  putChar(9);
}

fun printSpace() {
  putChar(32);
}

fun printColon() {
  putChar(58);
}

fun printMinus() {
  putChar(45);
}

fun printLine(length: i32 = 20) {
  printMinus();
  if length > 1 {
    printLine(length - 1);
  }
  else {
    printNextLine();
  }
}



fun modulo(number: i32, by: i32): i32 {
  return number - (number / by) * by;
}



/**
 * Print a char in the console.
 * Uses extern c putChar.
 */
fun extern putChar(c: i32)